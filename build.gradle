buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
    }
    ext {
        versions = [libs: [androidx: "1.2.0", billing: "3.0.3"],
                    sdk : [compile: 30, min: 14, target: 30],
                    code: 45,
                    name: "1.2.3"]
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    if (!project.hasProperty("nexusUsername")) {
        ext.nexusUsername = "no-username"
    }

    if (!project.hasProperty("nexusPassword")) {
        ext.nexusPassword = "no-password"
    }

    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    publishing {
        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username nexusUsername
                    password nexusPassword
                }
            }
        }
    }
}

ext.configurePublication = { MavenPublication pub, _packaging ->
    pub.groupId = 'org.solovyev.android'
    pub.version = versions.name

    pub.pom {
        name = 'Android Checkout Library'
        packaging = _packaging
        description = 'Library for Android In-App Billing (Version 3)'
        url = 'https://github.com/serso/android-checkout'

        scm {
            url = 'https://github.com/serso/android-checkout'
            connection = 'scm:https://serso@github.com/serso/android-checkout.git'
            developerConnection = 'scm:git://github.com/serso/android-checkout.git'
        }

        licenses {
            license {
                name = 'The Apache Software License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution = 'repo'
            }
        }

        developers {
            developer {
                id = 'se.solovyev'
                name = 'Sergey Solovyev'
                email = 'se.solovyev@gmail.com'
            }
        }
    }

    signing {
        sign pub
    }
}
