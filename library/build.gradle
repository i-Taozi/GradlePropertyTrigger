buildscript {
    repositories {
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.3'
        classpath 'org.robolectric:robolectric-gradle-plugin:1.0.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'org.robolectric'

robolectric {
    // configure the set of classes for JUnit tests
    include '**/*Test.class'
    exclude '**/espresso/**/*.class'

    // configure max heap size of the test JVM
    maxHeapSize = "2048m"
}

dependencies {
    repositories {
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        mavenCentral()
        mavenLocal()
    }

    compile 'com.esotericsoftware.kryo:kryo:2.20'

    testCompile 'junit:junit:4.10'
    testCompile('org.robolectric:robolectric:3.0-rc2') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testCompile 'com.squareup:fest-android:1.0.8'
}

android {
    compileSdkVersion 21
    buildToolsVersion '22.0.1'
}

// SONATYPE DEPLOYMENT

apply plugin: 'maven'
apply plugin: 'signing'

version = "0.9.2.5-SNAPSHOT"
group = "com.eyeem.potato"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: sonatypeSnapshotRepo) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'Potato'
            packaging 'aar'
            description 'Storage library for Android applications'
            url 'https://github.com/eyeem/potatolibrary'

            scm {
                url 'scm:git@github.com:eyeem/potatolibrary.git'
                connection 'scm:git@github.com:eyeem/potatolibrary.git'
                developerConnection 'scm:git@github.com:eyeem/potatolibrary.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'vishna'
                    name 'Lukasz Wisniewski'
                    email 'lukasz@eyeem.com'
                }
            }
        }
    }
}