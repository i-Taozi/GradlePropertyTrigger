apply plugin: 'com.android.application'

ext {
    useProduction = System.getenv("WS_USE_PRODUCTION") != null
}

repositories {
    google()
    mavenCentral()
}

android {
    buildToolsVersion "30.0.2" // also update in .travis.yml

    signingConfigs {
        dev {
            storeFile file("../keystores/dev.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        prod {
            if (System.getenv("WS_PROD_KEYSTORE_FILE") != null) {
                storeFile file(System.getenv("WS_PROD_KEYSTORE_FILE"))
                storePassword System.getenv("WS_PROD_KEYSTORE_PASSWORD")
                keyAlias System.getenv("WS_PROD_KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("WS_PROD_KEYSTORE_KEY_PASSWORD")
            } else {
                storeFile file("../keystores/dev.keystore")
                storePassword "android"
                keyAlias "androiddebugkey"
                keyPassword "android"
            }
        }
    }
    buildTypes {
        all {
            if (useProduction) {
                resValue 'string', 'app_name', 'Warmshowers'
                manifestPlaceholders = [applicationIcon: '@mipmap/ic_launcher']
                signingConfig signingConfigs.prod
            } else {
                applicationIdSuffix '.dev'
                versionNameSuffix '-dev'
                resValue 'string', 'app_name', 'Warmshowers (dev)'
                manifestPlaceholders = [applicationIcon: '@mipmap/ic_launcher_dev']
                signingConfig signingConfigs.dev
            }
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    lintOptions {
        error 'MissingTranslation'
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = isPreDexEnabled && !isTravisBuild
    }

    def versionMajor = 4
    def versionMinor = 0
    def versionPatch = 0
    def versionBuild = 0 // bump for dogfood builds, public betas, etc.

    defaultConfig {
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild

        minSdkVersion 16
        compileSdkVersion 30 // also update in .travis.yml
        targetSdkVersion 30

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    androidMaterialVersion = '1.3.0'
    androidSupportCompat = '28.0.0'
    androidxAppcompatVersion = '1.3.0'
    androidxWorkVersion = '2.5.0'
}

dependencies {
    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    implementation "androidx.appcompat:appcompat-resources:$androidxAppcompatVersion"
    implementation "androidx.work:work-runtime:$androidxWorkVersion"
    implementation "com.android.support:support-compat:$androidSupportCompat"
    implementation "com.google.android.material:material:$androidMaterialVersion"
}
